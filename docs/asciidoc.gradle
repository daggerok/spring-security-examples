buildscript {
  ext {
    asciidocBuildDir = "$buildDir/asciidoc"
  }
}

plugins {
  id "base"
  id "org.asciidoctor.convert" version "1.5.6"
  id "org.ajoberstar.git-publish" version "0.2.2"
}

group "daggerok"
version "0.0.1"

asciidoctorj {
  version = "1.5.5"
}

asciidoc {
  sourceDir = project.file("src")
  sources {
    include "index.adoc"
  }
  outputDir = project.file("$buildDir/asciidoc")
  attributes toc: "left",
      idprefix: "",
      docinfo1: "",
      icons: "font",
      idseparator: "-",
      setanchors: "true",
      "source-highlighter": "coderay",
      "toc-title": "Table of Contents",
      "build-gradle": file("build.gradle"),
      "endpoint-url": "https://daggerok.github.io/spring-security-examples"
}

asciidoctor.doLast {
  file("$asciidocBuildDir/html5/404.html").text = file("$asciidocBuildDir/html5/index.html").text
  file("$asciidocBuildDir/html5/.nojekyll").text = "\n"
}

gitPublish {

  repoUri = "git@github.com:daggerok/spring-security-examples.git"
  branch = "gh-pages"

  contents {
    from "$asciidocBuildDir/html5"
    from("$projectDir/src/static") {
      into "."
    }
  }

  commitMessage = "Documentation for ${project.group}:${project.name}:$project.version"
}

gitPublishPush.dependsOn asciidoctor
gitPublishPush.shouldRunAfter clean, asciidoctor, build

task docs(dependsOn: gitPublishPush)
